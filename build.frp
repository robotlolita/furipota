%furipota/1

import core "prelude"

# -- CONSTANTS
define js-sources    = find (path "source/**/*.js")
define ometa-sources = find (path "source/**/*.ometajs")
define project-root  = Path.directory self.path


# -- HELPERS
define discard stream = stream |> (x -> Stream.empty)

define npm-bin name = Stream.take 1 (find-upwards project-root (path "node_modules/.bin" / name))

define to-js x = Path.change-extension ".js" x

define ometa path @ options = do
  bind ometajs <- npm-bin "ometajs2js"
  action $(ometajs '--input path '--output options.output)
  return -from: path -to: options.output

define parents path =
  if Path.is-root path then
    [path]
  else
    [path, ...parents (Path.directory path)]

define find-upwards from pattern =
  Stream.from-vector (parents from)
    |> (base -> find pattern @ -base-directory: base  -return-absolute-paths: true)


define build-to build-dir =
  let    target dest   = build-dir / Path.relative (path "source") @ -to: dest
  in let show prefix x = Debug.log "{Path.to-text x.from} -> {Path.to-text x.to}" @ -prefix: prefix
  in let show-copy x   = show "COPY" x
  in let show-ometa x  = show "OMETA" x
  in do
    action remove build-dir | Debug.trace @ -prefix: "RMDIR"
    action make-directory build-dir | Debug.trace @ -prefix: "MKDIR"
    action parallel [
      js-sources |> (x -> copy x @ -to: (target x))
                 |> show-copy,

      do bind x <- ometa-sources
         action make-directory (Path.directory (target x)) | Debug.trace @ -prefix: "MKDIR"
         action (ometa x @ -output: (to-js (target x))) |> show-ometa
    ]


# --[ Tasks ]----------------------------------------------------------
export define snapshot =
  # Creates a snapshot of the Furipota VM. This is used for bootstrapping.
  build-to (path ".snapshot")

export define build =
  # Builds the Furipota VM
  build-to (path "furipota")

export define test =
  # Runs the tests for Furipota VM
  let show-error e = match e with
    case ^Shell.Error let e     then e.stack
    case ^Shell.Exit-Code let c then "The program exited with code {c}"
  in let furipota-run = project-root / "bin" / "furipota-run"
  in let run-shell s = Terminal.show-stream (Stream.map-errors show-error s)
  in sequential [
    do
      action Terminal.display-line "# Parser tests"
      bind test <- find (path "tests/**/run.js")
      action Debug.log (Path.to-text (Path.directory test)) @ -prefix: "TEST"
      action run-shell $('node test),

    do
      action Terminal.display-line "# Interpreter tests"
      bind test <- find (path "tests/interpreter/good/**/*.frp")
      action Debug.log (Path.to-text test) @ -prefix: "TEST"
      action run-shell $('node furipota-run test)
  ]
